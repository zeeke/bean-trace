buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
    dependencies {
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.3.1"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1'
        classpath 'net.researchgate:gradle-release:2.1.1'
    }
}

apply plugin: "com.github.kt3k.coveralls"
apply plugin: 'com.jfrog.bintray'
apply plugin: 'net.researchgate.release'

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'

group = 'org.laborra'

repositories {
    mavenLocal()
    mavenCentral()
}

sourceCompatibility = '7'
targetCompatibility = '7'

dependencies {
    compile 'com.google.guava:guava:18.0'

    testCompile "junit:junit:4.12",
            'org.hamcrest:hamcrest-all:1.3',
            'org.mockito:mockito-all:1.10.19',
            'com.google.guava:guava-testlib:18.0',
            "org.codehaus.groovy:groovy-all:2.4.1",
            "org.spockframework:spock-core:1.0-groovy-2.4",
            'org.assertj:assertj-core:2.1.0'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar
            artifact javadocJar
        }
    }
}

bintray {
    user = bintray_user
    key = bintray_api_key

    dryRun = false
    publish = true

    publications = ['mavenJava']

    pkg {
        repo = 'maven'
        group = project.group
        name =  project.name
        websiteUrl = 'https://github.com/zeeke/bean-trace'
        vcsUrl = 'https://github.com/zeeke/bean-trace.git'
        licenses = ['Apache-2.0']

        version {
            name = project.version
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

afterReleaseBuild.dependsOn bintrayUpload
